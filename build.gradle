plugins {
    id "java"
    id "java-library"
    id "maven-publish"
    id "com.google.protobuf" version "0.8.19"
}

publishing {
    repositories {
        maven {
            name = "GithubPackages"
            url = uri("https://maven.pkg.github.com/jessepinkman9900/grpc-interface-package")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
                password = project.findProperty("gpr.key") ?: System.getenv("TOKEN")
            }
        }
    }

    publications {
        gpr(MavenPublication) {
            from(components.java)
        }
    }
}

group = "sample.grpc.interface"
version = "1.2.0"
sourceCompatibility = JavaVersion.VERSION_15

repositories {
    mavenCentral()
}

def grpcVersion = '1.49.1'
def protobufVersion = '3.17.2'
def protocVersion = protobufVersion

dependencies {
    implementation("io.grpc:grpc-protobuf:${grpcVersion}")
    implementation("io.grpc:grpc-stub:${grpcVersion}")

    runtimeOnly("io.grpc:grpc-netty-shaded:${grpcVersion}")
    implementation("javax.annotation:javax.annotation-api:1.3.2")

    testImplementation("io.grpc:grpc-testing:${grpcVersion}")
    testImplementation("junit:junit:4.13.2")
}

protobuf {
    protoc { artifact = "com.google.protobuf:protoc:${protocVersion}"}
    plugins {
        grpc { artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"}
    }
    generateProtoTasks {
        all()*.plugins { grpc {}}
    }
}

test {
    useJUnitPlatform()
}
